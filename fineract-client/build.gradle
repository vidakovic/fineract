/**
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements. See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership. The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License. You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */
apply plugin: 'org.openapi.generator'

// TODO: @vidakovic unfortunately I can't make Gradle build the modules in the correct order (first fineract-provider, then fineract-client); this works for now
// ext['swaggerFile'] = "$rootDir/fineract-provider/build/classes/java/main/static/swagger-ui/fineract.yaml".toString()
ext['swaggerFile'] = "$projectDir/config/swagger/fineract.yaml".toString()

apply from: 'dependencies.gradle'

openApiMeta {
    generatorName = 'Fineract'
    packageName = 'org.apache.fineract.client'
    outputFolder = "$buildDir/meta".toString()
}

openApiValidate {
    // onlyIf file("$swaggerFile").exists()
    inputSpec = "$swaggerFile"
    recommend = true
}

task buildJavaSdk(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask){
    // onlyIf file("$swaggerFile").exists()
    generatorName = 'java'
    verbose = false
    validateSpec = false
    skipValidateSpec = true
    inputSpec = "$swaggerFile"
    outputDir = "$buildDir/generated/java".toString()
    groupId = 'org.apache.fineract'
    apiPackage = 'org.apache.fineract.client.services'
    invokerPackage = 'org.apache.fineract.client'
    modelPackage = 'org.apache.fineract.client.models'
    configOptions = [
        dateLibrary: 'java8',
        useRxJava2: 'true',
        library: 'retrofit2',
        hideGenerationTimestamp: 'true',
    ]
    generateModelTests = false
    generateApiTests = false
    templateDir = "$projectDir/src/main/templates/java/retrofit2".toString()
}

task buildGoSdk(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask){
    // onlyIf file("$swaggerFile").exists()
    generatorName = "go"
    verbose = false
    validateSpec = false
    skipValidateSpec = true
    inputSpec = "$swaggerFile"
    outputDir = "$buildDir/generated/go".toString()
    apiPackage = "fineract"
    configOptions = [
        dateLibrary: 'threetenp'
    ]
    generateModelTests = false
    generateApiTests = false
}

task buildZip(type: Zip) {
    into('fineract-client-go') {
        from "$buildDir/generated/go"
    }
}

compileJava.dependsOn buildJavaSdk, buildGoSdk, spotlessJavaApply, buildZip
buildJavaSdk.finalizedBy licenseFormat

sourceSets {
    generated {
        java {
            srcDir "$buildDir/generated/java/src/main/java"
        }
    }
}

compileJava {
    source += sourceSets.generated.java
}

configurations {
    generatedCompile.extendsFrom implementation
    generatedRuntime.extendsFrom runtime
}
