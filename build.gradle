/**
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements. See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership. The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License. You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */
buildscript {
    ext {
        jacocoVersion = '0.8.6'
        jerseyVersion = '1.19.4'
        groovyVersion = '3.0.6'
        springVersion = '5.2.9.RELEASE'
        springBootVersion = '2.3.4.RELEASE'
        retrofitVersion = '2.9.0'
        okhttpVersion = '4.7.2'
        oltuVersion = '1.0.1'
    }

    apply from: "$rootDir/gradle/repositories.gradle"

    dependencies {
        classpath 'com.bmuschko:gradle-cargo-plugin:2.7.1'
        classpath 'gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.14.0'
        classpath 'org.apache.openjpa:openjpa:3.1.2' // when upgrading, also change OpenJPA version repeated below in dependencyManagement!
        classpath 'com.radcortez.gradle:openjpa-gradle-plugin:3.1.0'
        classpath 'gradle.plugin.com.github.spotbugs.snom:spotbugs-gradle-plugin:4.5.1'
        classpath 'com.diffplug.spotless:spotless-plugin-gradle:5.6.1'
        classpath 'gradle.plugin.com.github.andygoossens:gradle-modernizer-plugin:1.3.0'
    }
}

plugins {
    id 'fr.brouillard.oss.gradle.jgitver' version '0.9.1'
    id 'org.hidetake.swagger.generator' version '2.18.2'
    id 'io.swagger.core.v3.swagger-gradle-plugin' version '2.1.5'
    // id 'com.github.andygoossens.gradle-modernizer-plugin' version '1.3.0'
    id 'com.github.hierynomus.license' version '0.15.0'
    id 'net.ltgt.errorprone' version '1.2.1'
    id 'org.springframework.boot' version "$springBootVersion"
    id "io.spring.dependency-management" version "1.0.10.RELEASE"
    id 'org.zeroturnaround.gradle.jrebel' version '1.1.10'
    id 'org.nosphere.apache.rat' version '0.7.0'
    id 'com.gorylenko.gradle-git-properties' version '2.2.3'
    id 'maven-publish'
    id 'org.openapi.generator' version '4.3.1'
    id 'org.asciidoctor.jvm.convert' version '3.2.0'
    id 'org.asciidoctor.jvm.pdf' version '3.2.0'
    id 'org.asciidoctor.jvm.epub' version '3.2.0'
    id 'org.asciidoctor.jvm.revealjs' version '3.2.0'
    id 'org.asciidoctor.jvm.gems' version '3.2.0'
    id 'org.asciidoctor.kindlegen.base' version '3.2.0'
    id 'io.github.lhotari.swagger2markup' version '1.3.3.1'
}

description = '''\
Run as:
gradle clean bootRun'''

ext['groovy.version'] = '3.0.6'

// apply from: 'gradle/sql.gradle'

allprojects  {
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'rebel'
    apply plugin: 'idea'
    apply plugin: 'project-report'
    apply plugin: 'fr.brouillard.oss.gradle.jgitver'

    group = 'org.apache.fineract'
    version = '0.0.0'

    apply from: "$rootDir/gradle/repositories.gradle"

    jgitver {
        strategy "PATTERN"
        // versionPattern "master".equals(System.getProperty("branch")) ? '${v}' : '${M}.${m}.${p}-${meta.GIT_SHA1_8}'
        versionPattern '${M}.${m}.${p}'
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework:spring-framework-bom:$springVersion"
            mavenBom "org.springframework.boot:spring-boot-dependencies:$springBootVersion"
            // TODO: @vidakovic make more use of BOMs instead of manually managing dependencies
            // mavenBom "org.springframework.security:spring-security-bom:$springSecurityVersion"
            // mavenBom "org.springframework.data:spring-data-releasetrain:$springDataVersion"
            // mavenBom "org.springframework.cloud:spring-cloud-dependencies:$springCloudVersion"
            // mavenBom "com.amazonaws:aws-java-sdk-pom:$awsSdkVersion"
            // mavenBom "org.eclipse.jetty:jetty-bom:$jettyVersion"
        }

        dependencies {
            // We use fixed versions, instead of inheriting them from the Spring BOM, to be able to be on more recent ones.
            // We do not use :+ to get the latest available version available on Maven Central, as that could suddenly break things.
            // We use the Renovate Bot to automatically propose Pull Requests (PRs) when upgrades for all of these versions are available.

            dependency 'org.springframework.security.oauth:spring-security-oauth2:2.5.0.RELEASE'
            dependency 'org.apache.openjpa:openjpa:3.1.2' // when upgrading, also change OpenJPA version repeated above in buildscript!
            dependency 'com.squareup.retrofit:retrofit:1.9.0'
            dependency 'com.squareup.okhttp:okhttp:2.7.5'
            dependency 'com.squareup.okhttp:okhttp-urlconnection:2.7.5'
            dependency 'com.google.guava:guava:29.0-jre'
            dependency 'com.google.code.gson:gson:2.8.6'
            dependency 'org.apache.commons:commons-email:1.5'
            dependency 'commons-io:commons-io:2.8.0'
            dependency 'org.drizzle.jdbc:drizzle-jdbc:1.4'
            dependency 'com.github.librepdf:openpdf:1.3.22'
            dependency 'org.mnode.ical4j:ical4j:3.0.19'
            dependency 'org.flywaydb:flyway-core:6.5.7'
            dependency 'org.quartz-scheduler:quartz:2.3.2'
            dependency 'com.amazonaws:aws-java-sdk-s3:1.11.874'
            dependency 'org.ehcache:ehcache:3.9.0'
            dependency 'com.github.spullara.mustache.java:compiler:0.9.6'
            dependency 'com.jayway.jsonpath:json-path:2.4.0'
            dependency 'org.apache.tika:tika-core:1.24.1'
            dependency 'org.apache.httpcomponents:httpclient:4.5.12'
            dependency 'io.swagger.core.v3:swagger-annotations:2.1.4'
            dependency 'jakarta.management.j2ee:jakarta.management.j2ee-api:1.1.4'
            dependency 'jakarta.jms:jakarta.jms-api:2.0.3'
            dependency 'jakarta.xml.bind:jakarta.xml.bind-api:2.3.3'
            dependency 'jakarta.validation:jakarta.validation-api:3.0.0'
            dependency 'org.apache.activemq:activemq-broker:5.16.0'
            dependency 'org.apache.bval:org.apache.bval.bundle:2.0.4'
            dependency 'org.mockito:mockito-core:3.5.13'
            dependency 'org.mockito:mockito-junit-jupiter:3.5.13'
            dependency 'io.github.classgraph:classgraph:4.8.90'
            dependency 'org.awaitility:awaitility:4.0.3'
            dependency 'com.github.spotbugs:spotbugs-annotations:4.1.3'
            dependency 'javax.cache:cache-api:1.1.1'
            dependency 'org.mock-server:mockserver-junit-jupiter:5.11.1'
            dependency 'org.webjars.npm:swagger-ui-dist:3.35.0'
            dependency 'org.webjars:webjars-locator-core:0.46'

            // fineract client dependencies
            dependency "com.squareup.retrofit2:retrofit:$retrofitVersion"
            dependency "com.squareup.retrofit2:retrofit-mock:$retrofitVersion"
            dependency "com.squareup.retrofit2:adapter-java8:$retrofitVersion"
            dependency "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
            dependency "com.squareup.retrofit2:adapter-rxjava3:$retrofitVersion"
            dependency "com.squareup.retrofit2:adapter-guava:$retrofitVersion"
            dependency "com.squareup.retrofit2:converter-wire:$retrofitVersion"
            dependency "com.squareup.retrofit2:converter-jackson:$retrofitVersion"
            dependency "com.squareup.retrofit2:converter-simplexml:$retrofitVersion"
            dependency "com.squareup.retrofit2:converter-jaxb:$retrofitVersion"
            dependency "com.squareup.retrofit2:converter-java8:$retrofitVersion"
            dependency "com.squareup.retrofit2:converter-scalars:$retrofitVersion"
            dependency "com.squareup.retrofit2:converter-gson:$retrofitVersion"
            dependency "com.squareup.retrofit2:converter-protobuf:$retrofitVersion"
            dependency "org.apache.oltu.oauth2:org.apache.oltu.oauth2.common:$oltuVersion"
            dependency "org.apache.oltu.oauth2:org.apache.oltu.oauth2.client:$oltuVersion"
            dependency "org.apache.oltu.oauth2:org.apache.oltu.oauth2.httpclient4:$oltuVersion"
            dependency "com.squareup.okhttp3:okhttp:$okhttpVersion"
            dependency "com.squareup.okhttp3:okcurl:$okhttpVersion"
            dependency "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"
            dependency "com.squareup.okhttp3:okhttp-apache:$okhttpVersion"
            dependency "com.squareup.okhttp3:okhttp-android-support:$okhttpVersion"
            dependency "com.squareup.okhttp3:okhttp-urlconnection:$okhttpVersion"
            dependency "com.squareup.okhttp3:okhttp-sse:$okhttpVersion"
            dependency "io.gsonfire:gson-fire:1.8.0"
            dependency "org.threeten:threetenbp:1.4.1"
            dependency "io.swagger:swagger-core:1.6.1"
            dependency "io.swagger:swagger-annotations:1.6.1"
            dependency "javax.annotation:javax.annotation-api:1.3.2"
            dependency "com.google.code.findbugs:jsr305:3.0.2"
            dependency "commons-codec:commons-codec:1.14"

            // tools, plugins
            dependency "com.google.errorprone:error_prone_core:2.4.0"
            dependency "jp.skypencil.findbugs.slf4j:bug-pattern:1.5.0"
            dependency 'com.github.spotbugs:spotbugs:4.1.3'
            dependency "org.apache.tomcat:tomcat:9.0.38"

            dependency ('org.dom4j:dom4j:2.1.3') {
                exclude 'relaxngDatatype:relaxngDatatype' // already in com.sun.xml.bind:jaxb-osgi:2.3.0.1
                // FINERACT-940 && FINERACT-966 https://github.com/spotbugs/spotbugs/issues/1128
                exclude 'xpp3:xpp3'
                exclude 'pull-parser:pull-parser'
            }

            dependencySet(group: 'com.sun.jersey', version: jerseyVersion) {
                entry 'jersey-core'
                entry 'jersey-servlet'
                entry 'jersey-server'
                entry 'jersey-json'
            }
            dependencySet(group: 'com.sun.jersey.contribs', version: jerseyVersion) {
                entry 'jersey-spring'
                entry 'jersey-multipart'
            }
            dependencySet(group: 'org.apache.poi', version: '4.1.2') {
                entry 'poi'
                entry 'poi-ooxml'
                entry 'poi-ooxml-schemas'
            }
            dependencySet(group: 'io.rest-assured', version: '4.3.0')
            {
                entry 'rest-assured'
                entry 'json-path'
                entry 'xml-path'
            }
        }
    }

    apply from: "$rootDir/gradle/eclipse.gradle"

    apply from: "$rootDir/gradle/license.gradle"

    apply from: "$rootDir/gradle/rat.gradle"

    apply from: "$rootDir/gradle/spotless.common.gradle"

    apply from: "$rootDir/gradle/errorprone.gradle"

    apply from: "$rootDir/gradle/spotbugs.gradle"
}

subprojects {
    apply from: "$rootDir/gradle/compile.gradle"

    apply from: "$rootDir/gradle/spotless.java.gradle"

    apply from: "$rootDir/gradle/jacoco.gradle"

    // apply from: "$rootDir/gradle/checkstyle.gradle"

    apply from: "$rootDir/gradle/gitproperties.gradle"

    apply from: "$rootDir/gradle/modernizer.gradle"
}
